buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'antlr'
apply plugin: 'jacoco'
apply plugin: 'kotlin'

repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

jacoco {
    toolVersion = "0.7.10-SNAPSHOT"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: 'com/nurkiewicz/tsclass/antlr/parser/**')
        })
    }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'commons-io:commons-io:2.5'
    compile 'org.projectlombok:lombok:1.16.18'
    compile 'com.google.guava:guava:21.0'
    compile 'org.ow2.asm:asm:5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

generateGrammarSource {
    arguments += ["-visitor", "-no-listener"]
    outputDirectory = new File("${buildDir}/generated-src/antlr/main/com/nurkiewicz/tsclass/antlr/parser")
}

check.dependsOn jacocoTestReport

compileKotlin.dependsOn 'generateGrammarSource'